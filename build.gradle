plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'com.testvagrant.ekam'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    // Apache
    implementation('org.apache.commons:commons-lang3:3.12.0')

    //Google
    implementation('com.google.guava:guava:30.1.1-jre')

    // Square
    implementation('com.squareup.retrofit2:retrofit:2.9.0')
    implementation('com.squareup.retrofit2:converter-gson:2.9.0')

    //lombok
    implementation('org.projectlombok:lombok:1.18.20')
    annotationProcessor('org.projectlombok:lombok:1.18.20')
    testImplementation('org.projectlombok:lombok:1.18.20')
    testAnnotationProcessor('org.projectlombok:lombok:1.18.20')

    // TestVagrant
    implementation('com.testvagrant.ekam:ekam-commons:0.0.6-beta')
    implementation('com.testvagrant.ekam:ekam-api:0.0.2-alpha')

    testImplementation('org.junit.jupiter:junit-jupiter-api:5.7.0')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.7.0')
}

test {
    useJUnitPlatform()
}


task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'com.testvagrant.ekam'
            artifactId 'ekam-device-manager'
            version '0.0.2-beta'

            from components.java

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

        }
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'Ekam Device Manager Project to discover and manage devices'
            name 'Ekam Device Manager'
            url 'https://gitlab.com/testvagrant/automation-platform/ekam-studio/ekam-device-manager'
            organization {
                name 'com.testvagrant.ekam'
                url 'https://gitlab.com/testvagrant/automation-platform/ekam-studio'
            }
            issueManagement {
                system 'GitLab'
                url 'https://gitlab.com/testvagrant/automation-platform/ekam-studio/ekam-device-manager/issues'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'https://gitlab.com/testvagrant/automation-platform/ekam-studio/ekam-device-manager/-/blob/master/LICENSE'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://gitlab.com/testvagrant/automation-platform/ekam-studio/ekam-device-manager'
                connection 'https://gitlab.com/testvagrant/automation-platform/ekam-studio/ekam-device-manager.git'
                developerConnection 'git@gitlab.com:testvagrant/automation-platform/ekam-studio/ekam-device-manager.git'
            }
            developers {
                developer {
                    name 'Sudarshan GS'
                }
                developer {
                    name 'Krishnanand B'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
}


signing {
    sign publishing.publications
}